<!doctype html>
<html lang="ja">
  <head>
  <meta charset="utf-8" />
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <meta name="path_to_root" content="..">
  <script type="text/javascript" src="../theme.js"></script>
  <script type="text/javascript" src="../script.js" defer></script>
  <script type="text/javascript" src="../metadata.js" defer></script>
  <script type="text/javascript" src="https://www.googletagmanager.com/gtag/js?id=G-9MVM0NZK9L" defer></script><script type="text/javascript" src="../gtag.js" defer></script>
  <link rel="stylesheet" href="../style.css" />
  
  <title>Excel のリンク付きが重い問題に対処する</title>
</head>

  <body>
    <header class="site-header">
  <div class="header-inner">
    <h1 class="site-title"><a href="../index.html">Sinotcaの雑記帳</a></h1>
    <div class="header-actions">
      <button
        id="search-toggle"
        class="header-button"
        type="button"
        onclick="toggleSearchInput()"
        aria-label="Toggle search"
        aria-controls="searchbar"
        aria-expanded="false"
      >
        <svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 512 512" width="20" height="20" aria-hidden="true" focusable="false">
          <!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.-->
          <path d="M416 208c0 45.9-14.9 88.3-40 122.7L502.6 457.4c12.5 12.5 12.5 32.8 0 45.3s-32.8 12.5-45.3 0L330.7 376c-34.4 25.2-76.8 40-122.7 40C93.1 416 0 322.9 0 208S93.1 0 208 0S416 93.1 416 208zM208 352a144 144 0 1 0 0-288 144 144 0 1 0 0 288z" />
        </svg>
      </button>
      <button
        id="theme-toggle"
        class="header-button"
        type="button"
        onclick="toggleTheme()"
        aria-label="Toggle color theme"
      >
        <svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 512 512" width="20" height="20" aria-hidden="true" focusable="false">
          <!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.-->
          <path d="M448 256c0-106-86-192-192-192v384c106 0 192-86 192-192zM0 256a256 256 0 1 1 512 0A256 256 0 1 1 0 256z" />
        </svg>
      </button>
    </div>
  </div>
  <form id="searchbar" class="site-search hidden" role="search" onsubmit="return false;">
    <input
      type="search"
      id="search-input"
      onkeyup="searchAndRender()"
      placeholder="Search this site"
      aria-label="Search site"
    >
    <button
      type="button"
      class="header-button search-close"
      onclick="toggleSearchInput()"
      aria-label="Close search"
    >
      &times;
    </button>
  </form>
  <div id="search-result" class="hidden"></div>
</header>

    <main>
      <div id="date">
        <svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 448 512" width="1em" height="1em" style="fill: var(--header-fg);">
          <!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.-->
          <path d="M152 24c0-13.3-10.7-24-24-24s-24 10.7-24 24l0 40L64 64C28.7 64 0 92.7 0 128l0 16 0 48L0 448c0 35.3 28.7 64 64 64l320 0c35.3 0 64-28.7 64-64l0-256 0-48 0-16c0-35.3-28.7-64-64-64l-40 0 0-40c0-13.3-10.7-24-24-24s-24 10.7-24 24l0 40L152 64l0-40zM48 192l352 0 0 256c0 8.8-7.2 16-16 16L64 464c-8.8 0-16-7.2-16-16l0-256z"/>
        </svg>
        2025-08-24 ~ 2025-08-24
      </div>
      <div> <a class="tag" href="../index.html?tag=excel">excel</a> <a class="tag" href="../index.html?tag=vba">vba</a></div>
      <div id="toc"></div>
      <div id="main-content">
        <h1 id="">Excel のリンク付きが重い問題に対処する</h1>
<pre><code>' =========================================================================================
' llp - Lightweight Linked Picture
'
'   llp insert     : コピー中のセル範囲（点線）→ リンク図を貼付け → 参照式を取得し
'                     LLP_{ID} = IF(LLP_UPDATE, &lt;範囲&gt;, "") を作成 → 図の式を =LLP_{ID} に差替
'   llp update     : LLP_UPDATE を TRUE→待機→FALSE にして全画像を更新
'   llp migrate    : 既存リンク/カメラ画像を LLP 名参照へ一括移行
'   llp clean      : どの図形からも参照されていない LLP_* 名称（孤児リンク）を即時一括削除（短縮 c）
'   llp resize     : 選択中の画像が1枚のときのみ、「図とサイズのリセット」を実行（短縮 r）
'   llp goto       : 選択中の画像が1枚のときのみ、参照先レンジへジャンプ（短縮 g）
'   llp localize   : LLP名の参照から外部ブック名（[Book.xlsx]）を一括削除（短縮 l）
' =========================================================================================

Option Explicit
Private Const MSO_PICTURE As Long = 13

Public Sub llp(Optional ByVal cmd As String = "")

    On Error GoTo FINALLY
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.DisplayAlerts = False

    InitLlpUpdateFlag
    If Len(cmd) = 0 Then cmd = ChooseCmdByPrompt()
    Select Case LCase$(cmd)
        Case "insert", "i": InsertLlp
        Case "update", "u": UpdateAll
        Case "migrate", "m": MigrateAll
        Case "clean", "c": CleanOrphanNames
        Case "resize", "r": ResizeSelected
        Case "goto", "g": GotoSelected
        Case "localize", "l": LocalizeBookRefs
    End Select

FINALLY:
    On Error GoTo 0
    SetLlpUpdateFlag False
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    Application.DisplayAlerts = True
    If ERR.Number &lt;&gt; 0 Then
        Application.StatusBar = "E" &amp; ERR.Number &amp; ": " &amp; ERR.Description
    End If
End Sub

Private Function ChooseCmdByPrompt() As String
    Dim s As Variant
    s = Application.InputBox( _
        Prompt:="llp サブコマンドを入力してください:" &amp; vbCrLf &amp; _
                "  migrate  : 既存リンク画像を新方式へ移行" &amp; vbCrLf &amp; _
                "  insert   : コピー中の範囲から作成（点線が前提）" &amp; vbCrLf &amp; _
                "  update   : 一斉パルス更新" &amp; vbCrLf &amp; _
                "  clean    : 孤児リンク(未使用の LLP_*)を即時一括削除" &amp; vbCrLf &amp; _
                "  resize   : 選択中1枚の画像を『図とサイズのリセット』" &amp; vbCrLf &amp; _
                "  goto     : 選択中1枚の画像の参照先へジャンプ" &amp; vbCrLf &amp; _
                "  localize : LLP名の参照から外部ブック名を一括削除", _
        Title:="llp - Lightweight Linked Picture", Type:=2)
    If VarType(s) = vbBoolean And s = False Then s = ""
    ChooseCmdByPrompt = CStr(s)
End Function

Private Sub InitLlpUpdateFlag()
    On Error Resume Next
    ActiveWorkbook.Names.Add name:="LLP_UPDATE", RefersTo:="=FALSE"
End Sub

Private Sub SetLlpUpdateFlag(ByVal state As Boolean)
    ActiveWorkbook.Names("LLP_UPDATE").RefersTo = "=" &amp; state
End Sub

Function GenId(Optional ByVal length As Long = 21) As String
    Const chars As String = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789"
    Dim i As Long
    Dim result As String
    Randomize Timer

    GenId = ""
    For i = 1 To length
        GenId = GenId &amp; Mid$(chars, Int(Rnd() * Len(chars)) + 1, 1)
    Next i
End Function

' LLP_UPDATE フラグが TRUE のときに f と評価される名前を作り、返す。
Private Function NewLlpName(ByVal f As String) As String
    NewLlpName = "LLP_" &amp; GenId()
    ActiveWorkbook.Names.Add name:=NewLlpName, RefersTo:="=IF(LLP_UPDATE," &amp; f &amp; ","""")"
End Function

Private Sub InsertLlp()
    Dim s As Shape
    Dim f As String

    If Application.CutCopyMode &lt;&gt; xlCopy Then
        ERR.Raise 601, "llp insert", "参照範囲をコピーした状態で実行してください。"
    End If

    ' リンクされた図を貼り付ける
    Application.CommandBars.ExecuteMso "PastePictureLink"
    Set s = ActiveSheet.Shapes(ActiveSheet.Shapes.Count)

    ' 貼り付けた図から、参照先を表す式を取得する
    f = GetFormulaOrEmpty(s)
    If Len(f) = 0 Then ERR.Raise 602, "llp insert", "参照範囲を取得できませんでした。"

    ' 範囲として評価できない式はセットできないので、一度フラグを TRUE にする
    SetLlpUpdateFlag True
    s.DrawingObject.formula = NewLlpName(f)
    SetLlpUpdateFlag False
End Sub

Private Sub UpdateAll()
    SetLlpUpdateFlag True
    Application.Wait Now + TimeSerial(0, 0, 1)
    SetLlpUpdateFlag False
End Sub

Private Sub MigrateAll()
    Dim ws As Worksheet
    Dim s As Shape
    Dim f As String

    SetLlpUpdateFlag True
    For Each ws In ActiveWorkbook.Worksheets
        For Each s In ws.Shapes
            f = GetFormulaOrEmpty(s)
            If Len(f) &lt;&gt; 0 And Not (f Like "LLP_*") Then
                s.DrawingObject.formula = "=" &amp; NewLlpName(f)
            End If
        Next s
    Next ws
    SetLlpUpdateFlag False
    Application.StatusBar = "移行が完了しました。"
End Sub

Private Sub CleanOrphanNames()
    Dim used As Object
    Dim ws As Worksheet
    Dim s As Shape
    Dim nm As name
    Dim llpName As String
    Set used = CreateObject("Scripting.Dictionary")

    ' 使用中の LLP_* を収集
    used.Add "LLP_UPDATE", True
    For Each ws In ActiveWorkbook.Worksheets
        For Each s In ws.Shapes
            llpName = GetLlpNameOrEmpty(s)
            If Not used.Exists(llpName) Then used.Add llpName, True
        Next s
    Next ws

    ' 未使用の LLP_* を削除（LLP_UPDATE は除外）
    For Each nm In ActiveWorkbook.Names
        If (nm.name Like "LLP_*") And (Not used.Exists(nm.name)) Then nm.Delete
    Next nm

    Application.StatusBar = "孤児リンクを削除しました。"
End Sub


Private Sub ResizeSelected()
    Dim shp As Shape
    Dim bk As String
    Dim sr As String

    On Error GoTo ERR
    For Each shp In Selection.ShapeRange
        sr = GetLlpSrcRangeOrEmptyS(shp)
        If Len(sr) = 0 Then GoTo NEXT_SHAPE

        ' 式を範囲に戻す
        bk = Trim$(shp.DrawingObject.formula)
        shp.DrawingObject.formula = "=" &amp; sr

        ' リサイズ
        shp.ScaleHeight 1, True   ' 高さをリセット
        shp.ScaleWidth 1, True    ' 幅をリセット

        ' 式を名前に戻す
        SetLlpUpdateFlag True
        shp.DrawingObject.formula = bk
        SetLlpUpdateFlag False
NEXT_SHAPE:
    Next shp
    Exit Sub
ERR:
    On Error GoTo 0
    SetLlpUpdateFlag True
    shp.DrawingObject.formula = bk
    SetLlpUpdateFlag False
    ERR.Raise 604, , "LLP画像を選択してください。"
End Sub


Private Sub GotoSelected()
    Dim src As String

    ' 選択中の画像の参照範囲を取得
    On Error GoTo ERR
    If (Selection.ShapeRange.Count &lt;&gt; 1) Then GoTo ERR
    src = GetLlpSrcRangeOrEmptyS(Selection.ShapeRange(1))
    If Len(src) = 0 Then GoTo ERR

    ' 選択範囲に移動
    Application.Goto Application.Range(src), True
    Exit Sub

ERR:
    On Error GoTo 0
    ERR.Raise 603, "llp goto ", "LLP 画像を1つだけ選択してください。"
End Sub

Private Sub LocalizeBookRefs()
    Dim nm As name
    Dim srcRange As String
    Dim srcRangeWoBk As String

    For Each nm In ActiveWorkbook.Names
        If nm.name = "LLP_UPDATE" Then GoTo NEXT_NAME
        If Not (nm.name Like "LLP_*") Then GoTo NEXT_NAME

        srcRange = GetLlpSrcRangeOrEmptyF(nm.RefersTo)
        If Len(srcRange) = 0 Then GoTo NEXT_NAME

        srcRangeWoBk = RemoveBookQualifier(srcRange)
        If srcRangeWoBk = srcRange Then GoTo NEXT_NAME

        nm.RefersTo = "=IF(LLP_UPDATE," &amp; srcRangeWoBk &amp; ","""")"
NEXT_NAME:
    Next nm

    Application.StatusBar = "LLP 画像から外部ブック名を削除しました。"
End Sub

' 画像から参照先の式を取得
Private Function GetFormulaOrEmpty(ByRef s As Shape) As String
    On Error Resume Next
    ' リンク付き画像は msoPicuture なので、 msoLinkedPicture は見なくて良い。
    If s.Type &lt;&gt; MSO_PICTURE Then Exit Function
    GetFormulaOrEmpty = Trim$(s.DrawingObject.formula)
    On Error GoTo 0
End Function

' LLP 管理下の Shape について、参照している名前を返す
Private Function GetLlpNameOrEmpty(ByRef s As Shape) As String
    Dim f As String
    f = GetFormulaOrEmpty(s)
    If f Like "LLP_*" Then GetLlpNameOrEmpty = f
    On Error GoTo 0
End Function


' LLP 管理下の Shape について、参照先の範囲を返す
Private Function GetLlpSrcRangeOrEmptyS(ByRef s As Shape) As String
    Dim nm As String
    Dim f As String
    nm = GetLlpNameOrEmpty(s)
    If Len(nm) = 0 Then Exit Function

    f = ActiveWorkbook.Names(Trim$(nm)).RefersTo
    GetLlpSrcRangeOrEmptyS = GetLlpSrcRangeOrEmptyF(f)
End Function

' LLP 式から参照先の範囲を取り出す
Private Function GetLlpSrcRangeOrEmptyF(ByRef f As String) As String
    Const PREFIX As String = "=IF(LLP_UPDATE,"
    Const SUFFIX As String = ","""")"
    If Not f Like (PREFIX &amp; "*") Then Exit Function
    If Not f Like ("*" &amp; SUFFIX) Then Exit Function
    GetLlpSrcRangeOrEmptyF = Mid$(f, Len(PREFIX) + 1, Len(f) - Len(PREFIX) - Len(SUFFIX))
End Function

' 範囲から 外部ブック修飾を除去
'  例:  "'[C:\path\[Book.xlsx]Sheet A'!$A$1" → "'Sheet A'!$A$1"
'       "[Book.xlsx]Sheet1!$A$1:$B$2"        → "Sheet1!$A$1:$B$2"
Function RemoveBookQualifier(ByRef RefText As String) As String
    Dim exclPos As Long
    Dim bracketPos As Long

    RemoveBookQualifier = RefText

    ' 末尾から ! を探す。
    exclPos = InStrRev(RefText, "!")
    If exclPos = 0 Then Exit Function

    ' ] を探す
    bracketPos = InStrRev(Left$(RefText, exclPos - 1), "]")
    If bracketPos = 0 Then Exit Function


    RemoveBookQualifier = Mid$(RefText, bracketPos + 1)

    ' ! の直前がシングルクォートなら、 ' 頭に ' を付与
    If Mid$(RefText, exclPos - 1, 1) = "'" Then
        RemoveBookQualifier = "'" &amp; RemoveBookQualifier
    End If
End Function
</code></pre>

      </div>
    </main>
    <footer>&copy; Sinotcaの雑記帳. All rights reserved.</footer>
  </body>
</html>
